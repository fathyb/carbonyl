import("//build/config/compiler/compiler.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "carbonyl.mojom" ]

  deps = [
    "//ui/gfx/geometry/mojom",
    "//skia/public/mojom",
  ]
}

component("blink") {
  sources = [
    "blink.cc",
    "blink.h",
  ]
}

component("carbonyl") {
  cflags_cc = ["-Wno-extra-semi"]
  sources = [
    "bridge.cc",
    "bridge.h",
    "host_display_client.cc",
    "host_display_client.h",
    "render_service_impl.cc",
    "render_service_impl.h",
    "software_output_device_proxy.cc",
    "software_output_device_proxy.h",
  ]

  deps = [
    ":mojom",
    "//base",
    "//components/viz/host",
    "//services/viz/privileged/mojom/compositing",
    "//ui/compositor",
    "//ui/display",
  ]

  target = ""

  if (current_cpu == "x64") {
    target += "x86_64-"
  } else if (current_cpu == "arm64") {
    target += "aarch64-"
  }

  if (is_mac) {
    target += "apple-darwin"
  } else if (is_linux) {
    target += "unknown-linux-gnu"
  }

  libs = ["carbonyl"]
  lib_dirs = ["//carbonyl/build/$target/release"]
}
